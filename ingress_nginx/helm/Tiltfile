"""Define the top-level `load_helmchart()` function."""

load("ext://helm_resource", "helm_repo", "helm_resource")

REPO_ALIAS = "ingress-nginx-repo"
LABEL = "ingress"

def load_helmchart(
        resource_deps = [],
        labels = [],
        chart_version = None,
        helm_flags = []):
    """
    Create a helm_resource from the ingress-nginx Helm Chart.

    Args:
        resource_deps: Specify resources that must exist before the controller
            may start. The Helm repo resource is added automatically.
        labels: Add the ingress controller to a group in the Tilt web UI.
            Default is `["ingress"]`.
        chart_version: Specify the version of the ingress-ningx Helm Chart to use.
            Default is latest.
        helm_flags: List of strings. Custom flags passed to `helm_resource()`.
    """
    helm_repo(
        REPO_ALIAS,
        url = "https://kubernetes.github.io/ingress-nginx",
        labels = labels or [LABEL],
    )

    default_helm_flags = [
        "--create-namespace",
        "--set=controller.allowSnippetAnnotations=true",
        "--set=controller.config.annotations-risk-level=Critical",
        "--set=controller.ingressClassResource.default=true",
    ]
    merged_helm_flags = default_helm_flags + helm_flags
    if chart_version:
        merged_helm_flags.append("--version=%s" % chart_version)

    helm_resource(
        "ingress-nginx",
        chart = "%s/ingress-nginx" % REPO_ALIAS,
        namespace = "ingress-nginx",
        flags = merged_helm_flags,
        resource_deps = [REPO_ALIAS] + resource_deps,
        labels = labels or [LABEL],
    )
