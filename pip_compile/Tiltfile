load("ext://uibutton", "cmd_button")


SCRIPTS = os.path.join(os.getcwd(), "scripts")
DEFAULT_COMPILE_ARGS = "--allow-unsafe --generate-hashes"


def pip_compile_button(
    image,
    target = "",
    reqs_path = ".",
    compile_args = "",
):
    """Add `pip-compile` button to all Tilt resources using `image`.

    This button operates very similar to the `cancel` button example in tilt-extensions:
    https://github.com/tilt-dev/tilt-extensions/blob/master/cancel/README.md
    """

    if config.tilt_subcommand == "down":
        return

    reqs_path = os.path.realpath(reqs_path)
    env_suffix = image.upper().replace("-", "_")

    if not compile_args:
        compile_args = DEFAULT_COMPILE_ARGS
    elif type(compile_args) == list:
        compile_args = " ".join(compile_args)

    local_resource(
        name="%s:pip-compile:operator" % image,
        serve_cmd = [
            "bash",
            os.path.join(SCRIPTS, "pip-compile-btn-controller.sh"),
            image,
        ],
        serve_env = {
            "PIP_COMPILE_TARGET_%s" % env_suffix: target,
            "PIP_COMPILE_REQSPATH_%s" % env_suffix: reqs_path,
            "PIP_COMPILE_ARGS_%s" % env_suffix: compile_args,
        },
        dir = SCRIPTS,
        labels = "support",
    )
